plugins {
    id 'com.gradleup.shadow'
    id "com.modrinth.minotaur" version "2.+"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-neoforge:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')

    modImplementation "com.cobblemon:neoforge:${project.cobblemon_version}"
    forgeRuntimeLibrary("thedarkcolour:kotlinforforge-neoforge:${project.kotlin_neo_version}") {
        exclude group: "net.neoforged.fancymodloader", module: "loader"
    }

    compileOnly("dev.emi:emi-neoforge:$project.emi_version:api")
    modRuntimeOnly("dev.emi:emi-neoforge:$project.emi_version")

}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: inputs.properties.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}

modrinth {
    token = modrinth_token
//    token = System.getenv("MODRINTH_TOKEN") // Please use an environment variable for this! The default is `$MODRINTH_TOKEN`.
    projectId = "9uOewQZ0" // The ID of your Modrinth project. Slugs will not work.
    versionNumber = "${project.mod_version}" // The (preferably SemVer) version of the mod. If not specified, it'll use the `version` declaration
    versionType =  "alpha"
    uploadFile = remapJar // Tells Minotaur to use the remapped jar
    gameVersions = ['1.21.1'] // An array of game versions the version supports
    dependencies {
        required.project "cobbleemistuff"
    }
    changelog = rootProject.file("CHANGELOG.md").text
    syncBodyFrom = rootProject.file("README.md").text
}